
--
-- Benchmarking: Sieve of Eratosthenes as done in Byte Sieve
--

library _gutil from "simvideo/gutil";

function sieve of none begin
	variable iters is u8 gets 64;
	variable screen is u8 pointer gets 0xF002_0000;
	variable sz is u32 gets (320 * 240) - 1;
	
	call _gutil.clear_screen with 0x00;
	
	for iter is u8 gets 1; iter <= iters; iter gets iter + 1 do
		variable t is u8 gets (((iter as u16) * 255) / (iters as u16)) as u8;
		
		for i is u32 gets 0; i <= sz; i gets i + 1 do
			screen index i gets t;
		end for
		
		for i is u32 gets 0; i <= sz; i gets i + 1 do
			if (screen index i) != 0 then
				variable prime is u32 gets i + i + 3;
				variable k is u32 gets i + prime;
				
				while k <= sz do
					screen index k gets 0x00;
					k gets k + prime;
				end while
			end if
		end for
	end for
end function
